
-- Simplified PageRank; assumes that all nodes have out degree > 0

alpha = [.85];
epsilon = [.0001];

Edge = SCAN(public:adhoc:edges, src:int, dst:int);
Vertex = SCAN(public:adhoc:vertices, id:int);

N = [FROM Vertex EMIT val=COUNT(id)];
min_rank = [(1 - *alpha) / *N];

OutDegree = [FROM Edge EMIT id=Edge.src, cnt=COUNT(Edge.dst)];
PageRank = [FROM Vertex EMIT id=Vertex.id, rank=1.0 / *N];

DO
    -- Calculate each node's outbound page rank contribution
    PrOut = [FROM PageRank, OutDegree WHERE PageRank.id == OutDegree.id
             EMIT id=PageRank.id, out_rank=PageRank.rank / OutDegree.cnt];

    -- Compute the inbound summands for each node
    Summand = [FROM Vertex, Edge, PrOut
                WHERE Edge.dst == Vertex.id AND Edge.src == PrOut.id
                EMIT id=Vertex.id, summand=PrOut.out_rank];
    
    -- Sum up the summands; adjust by alpha
    NewPageRank = [FROM Summand EMIT id=id,
                   rank=*min_rank + *alpha * SUM(Summand.summand)];
    Delta = [FROM NewPageRank, PageRank WHERE NewPageRank.id == PageRank.id
             EMIT val=ABS(NewPageRank.rank - PageRank.rank)];
    Continue = [FROM Delta EMIT MAX(Delta.val) > *epsilon];
    PageRank = NewPageRank;
WHILE Continue;

DUMP(PageRank);
